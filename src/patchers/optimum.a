;-------------------------------
; #Optimum
; self-overwriter that restores
; content later from custom sector
; e.g. StickyBear Math
;
; module by qkumba
;-------------------------------
!zone {
_optimum
         lda   gIsOptimum
         bne   ++
         lda   gTrack
         cmp   #2
         bne   +

         ;before read track 1, install hook

         lda   #<.readhook
         sta   $BE36
         lda   #>.readhook
         sta   $BE37
         lda   #$2C
         sta   $BE40

+        ldy   #15
         jsr   SearchTrack
         !byte $A2,$00,$A0,$00,$BD,$21,$03,$E8
         !byte $85,WILDCARD,$BD,$21,$03,$E8,$85
         bcs   ++
         ldy   #$01
         jsr   modify
         !byte $60
++       jmp   .exit

.table
         !byte $00,$01,$00,$07,$08,$09,$01,$02
         !byte $02,$00,$01,$06,$04,$03,$04,$03
         !byte $05,$05,$00,$02,$01,$0A,$07,$0E
         !byte $05,$02,$01,$06,$07,$03,$00,$02
         !byte $05,$0B,$04,$0F,$00,$07,$0A,$01
         !byte $02,$03,$00,$0C,$0C,$06,$07,$05
         !byte $08,$06,$02,$04,$01,$00,$08,$0A
         !byte $03,$0E,$00,$01,$07,$09,$0A,$05
         !byte $0B,$02,$05,$10,$03,$04,$0C,$06
         !byte $0D,$01,$0F,$06,$05,$0E,$0F,$0B
         !byte $11,$02,$00,$09

.readhook
         ldy   #$20
--       dey
         beq   +
-        lda   $C08C, x
         bpl   -
---      eor   #$D5
         bne   --
         nop
-        lda   $C08C, x
         bpl   -
         cmp   #$AA
         bne   ---
-        lda   $C08C, x
         bpl   -
         nop
         nop
         sta   $C08D, x
         ldy   #$56
         sty   $26 ;consume cycles
-        lda   $C08C, x
         bpl   -
         dey
         sta   $BC00, y
         bne   -
-        lda   $C08C, x
         bpl   -
         sta   $BB00, y
         iny
         bne   -

         ;decoder

         ldx   #$BC
         stx   .tblsel1+2
         stx   .tblsel2+2
         lda   #$88 ;DEY
         sta   .direction1
         sta   .direction2
         lda   #$86
         sta   $26
         ldy   #$53
-
.tblsel1
         ldx   $BC00, y
         lda   (.table-$96)-$16, x
         asl
         asl
         asl
         asl
.direction1
         dey
.tblsel2
         ldx   $BC00, y
         ora   (.table-$96)-$16, x
         sty   $2C
         ldy   $26
         sta   ($3E), y
         ldy   $2C
         bne   +
         dec   .tblsel1+2
         dec   .tblsel2+2
         lda   #$C8 ;INY
         sta   .direction1
         sta   .direction2
         !byte $24 ;mask DEY
+
.direction2
         dey
         dec   $26
         bne   -

         ;after read track 1, uninstall hook

         lda   #$DC
         sta   $BE36
         lda   #$B8
         sta   $BE37
         lda   #$20
         sta   $BE40
         pla
         pla
         jmp   $BE43

.exit
}
