;-------------------------------
; #Optimum
; self-overwriter that restores
; content later from custom sector
; e.g. StickyBear Math
;
; module by qkumba
;-------------------------------
!zone {
_optimum
         lda   gIsOptimum
         bne   .jmpexit1
         ldy   #15
         jsr   SearchTrack
         !byte $A2,$00      ;LDX #$00
         !byte $A0,$00      ;LDY #$00
         !byte $BD,WILDCARD,$03  ;LDA $03xx,X
         !byte $E8          ;INX
         !byte $85,WILDCARD ;STA $xx
         !byte $BD,WILDCARD,$03  ;LDA $03xx,X
         !byte $E8          ;INX
         !byte $85          ;STA $xx
         bcc   +
         lda   gTrack
         beq   .jmpreread

.jmpexit1
         jmp   .exit

.jmpreread
         jmp   .reread1

+        pha
         adc   #BASEPAGE
         sta   .swapbase2+2
         tay
         sbc   #0
         sta   .swapbase1+1
         txa
         pha
         dey
         sty   .swapbase5+2

.swapbase1
         lda   #$D1
         ldy   #5
         jsr   SearchSector
         !byte $00,$07,$08,$09,$01
         bcs   .notable
         txa
         sbc   #($96+$17)
         sta   .swapbase3+1
         sta   .swapbase4+1
         lda   .swapbase1+1
         sbc   #0
         sta   .swapbase3+2
         sta   .swapbase4+2
         ldy   #0
-
.swapbase2
         lda   $D100,y
         sta   $2D8,y
         iny
         bne   -

         lda   #2
         sta   .retrycount

         lda   gTrack
         pha
         lda   #1
         sta   gTrack

         ;disable decoding because using the real version
         ;would corrupt the text screen

.retry
         lda   #$2C
         sta   $BE40

         ;read T01S0F, remains encoded at $BB00-BC55

         ldy   #<gRWTSParams
         lda   #>gRWTSParams

         ;special case check

         ldx   $2E2
         cpx   #$48 ;PHA
         beq   +
         jsr   $2FC
         jmp   ++

         ;regular dispatch
+
         jsr   $2E2

++
         ;re-enable decoding

         lda   #$20
         sta   $BE40

         ;detect read failure

         bcc   +
         dec   .retrycount
         bne   .retry
         lda   #s_optbad  ; say read failure
         jsr   PrintByID  ; but can't quit from here
         pla
         sta   gTrack
.notable
         pla
         pla
         jmp   .exit

+
         pla
         sta   gTrack

         ;disable redirection

         pla
         tax
         pla
         ldy   #$01
         jsr   modify
         !byte $60          ;RTS

         ;decode and stash content

         lda   #$BC
         sta   .tblsel1+2
         sta   .tblsel2+2
         lda   #$88
         sta   .direction1
         sta   .direction2
         lda   #$86
         sta   $26
         ldy   #$53
-
.tblsel1
         ldx   $D100,y
.swapbase3
         lda   $D1D1,x
         asl
         asl
         asl
         asl
.direction1
         dey
.tblsel2
         ldx   $D100,y
.swapbase4
         ora   $D1D1,x
         sty   $2C
         ldy   $26
         sta   $300,y
         ldy   $2C
         bne   +
         dec   .tblsel1+2
         dec   .tblsel2+2
         lda   #$C8
         sta   .direction1
         sta   .direction2
         !byte $24 ;mask DEY
+
.direction2
         dey
         dec   $26
         ldx   $26
         inx
         bne   -

         ;read IOB with replaced page

.swapbase5
         lda   $D16D,x
         sta   .address+1

         lda   gTrack
         beq   .reread1

.jmpexit2
         jmp   .exit

.retrycount
         !byte $D1

         ;track 0, find the region with replaced page

.reread1
         ldy   #8
         jsr   SearchTrack
         !byte $20,$5D,$B6  ;JSR $B65D
         !byte WILDCARD, WILDCARD
         !byte $00,$08,$18  ;address, count
         bcs   .jmpexit2 ;give up
         adc   #BASEPAGE
         sta   .patch1+2
         sta   .patch2+2
         lda   #s_construct
         jsr   PrintByID

         ;convert address to track/sector

.address
         lda   #$D1
         sbc   #7
         pha
         and   #$0F
         sta   tmp
.patch1
         lda   $D104,x
         sbc   tmp
         and   #$0F
         sta   gSector
         pla
         php
         lsr
         lsr
         lsr
         lsr
         clc
.patch2
         adc   $D103,x
         tay
         plp
         bcs   +
         iny
+        sty   gTrack

         ;read replaced page

         lda   #9         ; $900-9FF
         sta   gAddress+1
         ldy   #<gRWTSParams
         lda   #>gRWTSParams
         jsr   $BD00

         ;merge content

         ldy   #$86
-        lda   $300,y
         sta   $900,y
         dey
         cpy   #$FF
         bne   -

         ;read other page in block

         lda   #1
         sta   gTrack
         sta   gSector
         dec   gAddress+1 ; and $800-8FF
         ldy   #<gRWTSParams
         lda   #>gRWTSParams
         jsr   $BD00

         ;write replaced block

         lda   #8         ; $800-9FF
         sta   mliparam+3 ; hi byte of data buffer
         dec   mliparam+4 ; lo byte of block number
         jsr   SwapProDOS
         lda   #$81       ; 'write block' command
         ldy   #$03       ; parameter count
         jsr   mli
         jsr   SwapProDOS

         ;restore original disk location

         dec   gTrack     ; #0
         lda   #$0F
         sta   gSector

.exit
}
